Imports: {
	import static gate.Utils.*;
	import BHI.ReportExtractor.JapeRhsProcessor;
}



Phase: AortaStjVisualDiameter
Input: Token Anatomy VarSentence Units Categorical Numeric Lookup Split
Options: control=Appelt negationGrouping=false

Macro: CONTEXT
(
	{Anatomy,
		Anatomy within {VarSentence.minorType == "sinotubular_junction"},
		Anatomy.minorType == "sinotubular_junction"}	
)

Macro: CONTEXT_OTHER_AORTA
(
	({Lookup.minorType == "conjunction"} | {Lookup.minorType == "preposition"} | {Token.string=="/"} | {Token.string==","})?
	{Anatomy,
		Anatomy within {VarSentence.minorType == "sinotubular_junction"},
		Anatomy.majorType == "aorta"}
	({Lookup.minorType == "conjunction"} | {Lookup.minorType == "preposition"} | {Token.string=="/"} | {Token.string==","})?
)

Macro: FILTER
(
	{Token,
		!Anatomy,
		!Lookup.minorType == "conjunction",
		!Categorical,
		!Split.kind == "external",
		Token within {VarSentence.minorType == "sinotubular_junction"}}
)

Macro: FILTER_ALLOW_NEG_MODIF
(
	{Token,
		!Anatomy,
		!Lookup.minorType == "conjunction",
		!Categorical.minorType != "negative_modifier",
		!Split.kind == "external",
		Token within {VarSentence.minorType == "sinotubular_junction"}}
)

Macro: VALUE
(
	({Categorical, 
		Categorical.minorType == "dilated", 
		Categorical within {VarSentence.minorType == "sinotubular_junction"}}) |
	({Categorical, 
		Categorical.minorType == "nondilated",
		Categorical within {VarSentence.minorType == "sinotubular_junction"}})
)

Macro: MODIFIER
(
	{Categorical, 
		Categorical.minorType == "negative_modifier",
		Categorical within {VarSentence.minorType == "sinotubular_junction"}}
)

/*
 * Description:
 * Of the forms...
 * e.g. 'StJ is not dilated for height'
 * e.g. 'Sov, Stj, Asc ao are not dilated'
 * e.g. 'Stj and SOV are not dilated'
 */
Rule: ao_stj_cat_1
Priority: 100
(
	(
		(CONTEXT (CONTEXT_OTHER_AORTA)[0,2]) |
		((CONTEXT_OTHER_AORTA)[0,2] CONTEXT) |
		((CONTEXT_OTHER_AORTA)[0,1] CONTEXT (CONTEXT_OTHER_AORTA)[0,1])
	):context
	
	( FILTER_ALLOW_NEG_MODIF )[0,20]
			
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,2]

	( VALUE ):value
	
):ao_stj_cat 
--> 
:ao_stj_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_stj_cat", doc, bindings, outputAS);
}

/*
 * Description:
 * Of the forms...
 * e.g. 'Non-dilated StJ'
 * e.g. 'No evidence of significant dilation of the StJ'
 * e.g. 'Not dilated at the StJ'
 * e.g. 'Non-dilated aortic root at the STJ'
 * e.g. 'Non-dilated aortic root at the Sov and Stj'
 */
Rule: ao_stj_cat_2
Priority: 99
(
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,3]

	( VALUE ):value
	
	(
		{Anatomy,
			Anatomy.minorType == "aortic_root",
			Anatomy within {VarSentence.minorType == "sinotubular_junction"}}
		{Lookup.minorType == "preposition"}
	)?
			
	( FILTER )[0,3]
	
	(
		(CONTEXT (CONTEXT_OTHER_AORTA)[0,2]) |
		((CONTEXT_OTHER_AORTA)[0,2] CONTEXT) |
		((CONTEXT_OTHER_AORTA)[0,1] CONTEXT (CONTEXT_OTHER_AORTA)[0,1])
	):context
	
):ao_stj_cat 
--> 
:ao_stj_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_stj_cat", doc, bindings, outputAS);
}

/*
 * Description:
 * Of the forms...
 * e.g. 'Non-dilated StJ'
 * e.g. 'Not dilated at the StJ'
 * e.g. 'Non-dilated aortic root at the StJ'
 * e.g. 'Non-dilated ascending aorta at the StJ level'
 * e.g. 'Non dilated proximal aorta to StJ level.'
 * e.g. 'Non-dilated aortic root at the StJ and Asc Ao'
 * e.g. 'Non dilated proximal aorta (3.1cm at the level of the STJ).'
 */
Rule: ao_stj_cat_3
Priority: 98
(
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,3]

	( VALUE ):value
	
	(
		({Anatomy,
			Anatomy.minorType == "aortic_root",
			Anatomy within {VarSentence.minorType == "sinotubular_junction"}} |
		 {Anatomy,
			Anatomy.minorType == "ascending_aorta",
			Anatomy within {VarSentence.minorType == "sinotubular_junction"}})
		
		( FILTER )[0,5]
				
		{Lookup.minorType == "preposition"}
	)?
			
	( FILTER )[0,4]
	
	(
		(CONTEXT (CONTEXT_OTHER_AORTA)[0,2]) |
		((CONTEXT_OTHER_AORTA)[0,2] CONTEXT) |
		((CONTEXT_OTHER_AORTA)[0,1] CONTEXT (CONTEXT_OTHER_AORTA)[0,1])
	):context
	
):ao_stj_cat 
--> 
:ao_stj_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_stj_cat", doc, bindings, outputAS);
}

/*
 * Description:
 * Of the form...
 * e.g. 'Sinotubular junction 4.1cm, proximal ascending aorta 4.3cm, neither are dilated'
 * e.g. 'SoV 4.1cm, Stj 4.1cm, Asc Ao 4.1cm, all are dilated'
 */
Rule: ao_stj_cat_4
Priority: 97
(
	( CONTEXT ):context	
	
	( {Token,
		!Categorical,
		!Split.kind == "external",
		Token within {VarSentence.majorType == "aorta"}} )[0,20]

	( {Lookup.minorType == "distributive_determiner"} |
	  
	  ({Lookup.minorType == "distributive_determiner",
	  		Categorical.minorType == "negative_modifier",
	  		Categorical within {VarSentence.majorType == "aorta"}}):modifier )
	
	( {Token,
		!Anatomy,
		!Categorical,
		!Split.kind == "external",
		Token within {VarSentence.majorType == "aorta"}} )[0,2]

	( ({Categorical, 
		Categorical.minorType == "dilated", 
		Categorical within {VarSentence.majorType == "aorta"}}) |
	  ({Categorical, 
		Categorical.minorType == "nondilated",
		Categorical within {VarSentence.majorType == "aorta"}}) ):value
	
):ao_stj_cat 
--> 
:ao_stj_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_stj_cat", doc, bindings, outputAS);
}

/*
 * Description:
 * Of the forms...
 * e.g. 'Sov to Asc Ao is dilated'
 * e.g. 'Sov - Asc Ao is nondilated'
 */
Rule: ao_stj_cat_5
Priority: 96
(
	(
		{Anatomy,
			Anatomy within {VarSentence.majorType == "aorta"},
			Anatomy.minorType == "sinus_of_valsalva"}
		{Token,
			Token within {VarSentence.majorType == "aorta"},
			Token.string ==~ "to|[-]"}
		{Anatomy,
			Anatomy within {VarSentence.majorType == "aorta"},
			Anatomy.minorType == "ascending_aorta"}
	):context
	
	( {Token,
		!Anatomy,
		!Categorical,
		!Split.kind == "external",
		Token within {VarSentence.majorType == "aorta"}} )[0,2]
			
	( 
		({Categorical, 
			Categorical.minorType == "negative_modifier",
			Categorical within {VarSentence.majorType == "aorta"}})? 
	):modifier

	( 
		({Categorical, 
			Categorical.minorType == "dilated", 
			Categorical within {VarSentence.majorType == "aorta"}}) |
		({Categorical, 
			Categorical.minorType == "nondilated",
			Categorical within {VarSentence.majorType == "aorta"}}) 
	):value
	
):ao_stj_cat 
--> 
:ao_stj_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_stj_cat", doc, bindings, outputAS);
}

/*
 * Description:
 * Of the forms...
 * e.g. 'Dilated aortic root from SoV to prox. ascending aorta'
 */
Rule: ao_stj_cat_6
Priority: 95
(
	( 
		({Categorical, 
			Categorical.minorType == "negative_modifier",
			Categorical within {VarSentence.majorType == "aorta"}})? 
	):modifier
	
	( {Token,
		!Anatomy,
		!Categorical,
		!Split.kind == "external",
		Token within {VarSentence.majorType == "aorta"}} )[0,2]

	( 
		({Categorical, 
			Categorical.minorType == "dilated", 
			Categorical within {VarSentence.majorType == "aorta"}}) |
		({Categorical, 
			Categorical.minorType == "nondilated",
			Categorical within {VarSentence.majorType == "aorta"}}) 
	):value
	
	(
		({Anatomy,
			Anatomy.minorType == "aortic_root",
			Anatomy within {VarSentence.majorType == "aorta"}} |
		 {Anatomy,
			Anatomy.minorType == "ascending_aorta",
			Anatomy within {VarSentence.majorType == "aorta"}})
		{Lookup.minorType == "preposition"}
	)?
			
	( {Token,
		!Anatomy,
		!Categorical,
		!Split.kind == "external",
		Token within {VarSentence.majorType == "aorta"}} )[0,3]
	
	(
		{Anatomy,
			Anatomy within {VarSentence.majorType == "aorta"},
			Anatomy.minorType == "sinus_of_valsalva"}
		{Token,
			Token within {VarSentence.majorType == "aorta"},
			Token.string ==~ "to|[-]"}
		{Anatomy,
			Anatomy within {VarSentence.majorType == "aorta"},
			Anatomy.minorType == "ascending_aorta"}
	):context
	
):ao_stj_cat 
--> 
:ao_stj_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_stj_cat", doc, bindings, outputAS);
}