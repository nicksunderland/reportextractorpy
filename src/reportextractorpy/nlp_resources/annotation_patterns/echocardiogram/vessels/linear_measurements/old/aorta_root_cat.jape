Imports: {
	import static gate.Utils.*;
	import BHI.ReportExtractor.JapeRhsProcessor;
}



Phase: AortaVisualDiameter
Input: Anatomy Numeric Units Lookup Token VarSentence Categorical Split
Options: control=Appelt negationGrouping=false

Macro: CONTEXT
(
	{Anatomy,
		Anatomy within {VarSentence.minorType == "aortic_root"},
		Anatomy.minorType == "aortic_root"}
)

Macro: CONTEXT_OTHER_AORTA
(
	({Lookup.minorType == "conjunction"} | {Lookup.minorType == "preposition"} | {Token.string=="/"} | {Token.string==","})?
	{Anatomy,
		Anatomy within {VarSentence.minorType == "aortic_root"},
		Anatomy.majorType == "aorta"}
	({Lookup.minorType == "conjunction"} | {Lookup.minorType == "preposition"} | {Token.string=="/"} | {Token.string==","})?
)

Macro: FILTER
(
	{Token,
		!Anatomy,
		!Lookup.minorType == "conjunction",
		!Categorical,
		!Split.kind == "external",
		Token within {VarSentence.minorType == "aortic_root"}}
)

Macro: FILTER_ALLOW_NEG_MODIF
(
	{Token,
		!Anatomy,
		!Lookup.minorType == "conjunction",
		!Categorical.minorType != "negative_modifier",
		!Split.kind == "external",
		Token within {VarSentence.minorType == "aortic_root"}}
)

Macro: VALUE
(
	({Categorical, 
		Categorical.minorType == "dilated", 
		Categorical within {VarSentence.minorType == "aortic_root"}}) |
	({Categorical, 
		Categorical.minorType == "nondilated",
		Categorical within {VarSentence.minorType == "aortic_root"}})
)

Macro: MODIFIER
(
	{Categorical, 
		Categorical.minorType == "negative_modifier",
		Categorical within {VarSentence.minorType == "aortic_root"}}
)

/*
 * Description:
 * block general references to the aortic root followed by a more 
 * specific reference to part of the aortic root
 * e.g. 'Aortic root at sinus of valsalva is not dilated'
 * e.g. 'Aortic root at the level of the sinus of valsalva...'
 */
Rule: ao_root_blocker_1
Priority: 101
(	
	( CONTEXT ):context
	
	{Token.string ==~ "(?i)at"}
	
	( FILTER )[0,4]
	
	( CONTEXT_OTHER_AORTA )
	
	( FILTER_ALLOW_NEG_MODIF )[0,20]
			
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,5]

	( VALUE ):value
			
):ao_root_blocker
-->
:ao_root_blocker.Blocked = {type = "ao_root_cat_blocker"}

/*
 * Description:
 * block general references to the aortic root followed by a more 
 * specific reference to part of the aortic root
 * e.g. 'Aortic root is upper limits of normal at sinus of valsalva'
 */
Rule: ao_root_blocker_1
Priority: 101
(	
	( CONTEXT ):context
	
	( FILTER_ALLOW_NEG_MODIF )[0,10]
			
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,2]

	( VALUE ):value
	
	{Lookup.minorType == "preposition"}
	
	( FILTER )[0,2]
	
	( CONTEXT_OTHER_AORTA )
			
):ao_root_blocker
-->
:ao_root_blocker.Blocked = {type = "ao_root_cat_blocker"}

/*
 * Description:
 * block general references to the aortic root followed by a more 
 * specific reference to part of the aortic root
 * e.g. 'Dilated aortic root at sinus of valsalva'
 * e.g. 'No significant evidence of dilation of the aortic root at sinus of valsalva'
 */
Rule: ao_root_blocker_2
Priority: 101
(	
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,3]
	
	( VALUE ):value
	
	( FILTER )[0,2]
	
	( CONTEXT ):context
		
	{Lookup.minorType == "preposition"}
	
	( FILTER )[0,2]
	
	( CONTEXT_OTHER_AORTA )
			
):ao_root_blocker
-->
:ao_root_blocker.Blocked = {type = "ao_root_cat_blocker"}

/*
 * Description:
 * block general references to the aortic root followed by a more 
 * specific reference to part of the aortic root
 * e.g. 'Aortic root: nondilated sinus of valsalva'
 */
Rule: ao_root_blocker_3
Priority: 101
(	
	( CONTEXT ):context
	
	( {Token.string ==~ "[:-]"} | {Split.kind == "external"} )
	
	( FILTER_ALLOW_NEG_MODIF )[0,10]
			
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,2]

	( VALUE ):value
	
	( CONTEXT_OTHER_AORTA )
			
):ao_root_blocker
-->
:ao_root_blocker.Blocked = {type = "ao_root_cat_blocker"}

/*
 * Description:
 * Of the forms...
 * e.g. 'root is not dilated for height'
 * e.g. 'the root and Asc ao are not dilated'
 */
Rule: ao_root_cat_1
Priority: 100
(
	(
		(CONTEXT (CONTEXT_OTHER_AORTA)[0,2]) |
		((CONTEXT_OTHER_AORTA)[0,2] CONTEXT) |
		((CONTEXT_OTHER_AORTA)[0,1] CONTEXT (CONTEXT_OTHER_AORTA)[0,1])
	):context
	
	( FILTER_ALLOW_NEG_MODIF )[0,20]
			
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,5]

	( VALUE ):value
	
):ao_root_cat 
--> 
:ao_root_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_root_cat", doc, bindings, outputAS);
}


/*
 * Description:
 * Of the forms...
 * e.g. 'Non-dilated root'
 * e.g. 'Not dilated at the root'
 */
Rule: ao_root_cat_2
Priority: 99
(
	( (MODIFIER)? ):modifier
	
	( FILTER )[0,3]

	( VALUE ):value
	
	( FILTER )[0,3]
	
	(
		(CONTEXT (CONTEXT_OTHER_AORTA)[0,2]) |
		((CONTEXT_OTHER_AORTA)[0,2] CONTEXT) |
		((CONTEXT_OTHER_AORTA)[0,1] CONTEXT (CONTEXT_OTHER_AORTA)[0,1])
	):context
	
):ao_root_cat 
--> 
:ao_root_cat
{
	/* Creation of the processor object does an 'in-place' processing of the annotations found using the above rules.
	 * Using the bindings, strings are extracted from the doc, processed/parsed, then inserted into the outputAS (output
	 * annotation set).	
	 */
	JapeRhsProcessor processor = new JapeRhsProcessor("echocardiogram", "ao_root_cat", doc, bindings, outputAS);
}
